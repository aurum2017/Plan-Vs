функция ОпределениеКоэффициентовТренда() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(тз.Период КАК ДАТА) КАК Период,
	|	тз.Номенклатура КАК Номенклатура,
	|	тз.Клиент КАК Клиент,
	|	тз.Количество КАК Количество,
	|	тз.Среднее КАК Среднее
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&тз КАК тз
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), МЕСЯЦ) КАК Период,
	|	СУММА(ЕСТЬNULL(вт.Среднее, 0)) КАК Количество,
	|	вт.Номенклатура КАК Номенклатура,
	|	вт.Клиент КАК Клиент
	|ИЗ
	|	вт КАК вт
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), МЕСЯЦ) >= &ДатаНачала
	|	И НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), МЕСЯЦ) <= &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), МЕСЯЦ),
	|	вт.Номенклатура,
	|	вт.Клиент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Номенклатура,
	|	Клиент,
	|	Период ПЕРИОДАМИ(МЕСЯЦ, &ДатаНачала, &ДатаОкончания)";
КонецФункции

функция ПомесячныеДанныеПродаж()Экспорт
	Возврат
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ДанныеПродаж.Период, МЕСЯЦ) КАК Период,
	|	ДанныеПродаж.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ДанныеПродаж.Клиент.ВИП
	|			ТОГДА ДанныеПродаж.Клиент
	|		ИНАЧЕ ЗНАЧЕНИЕ(справочник.партнеры.остальные)
	|	КОНЕЦ КАК Клиент,
	|	СУММА(ДанныеПродаж.Количество) КАК Количество
	|ИЗ
	|	РегистрСведений.ДанныеПродаж КАК ДанныеПродаж
	|ГДЕ
	|	ДанныеПродаж.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ДанныеПродаж.Период, МЕСЯЦ),
	|	ДанныеПродаж.Номенклатура,
	|	ВЫБОР
	|		КОГДА ДанныеПродаж.Клиент.ВИП
	|			ТОГДА ДанныеПродаж.Клиент
	|		ИНАЧЕ ЗНАЧЕНИЕ(справочник.партнеры.остальные)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Номенклатура,
	|	Клиент,
	|	Период ПЕРИОДАМИ(МЕСЯЦ, &ДатаНачала, &ДатаОкончания)";
КонецФункции

функция ЛобовойПрогноз() Экспорт
	возврат
	"ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 0) КАК Период
	|ПОМЕСТИТЬ втПериодыПланирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 2)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 3)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 4)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 5)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 6)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 7)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 8)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 9)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, 11)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериодыПланирования.Период КАК Период,
	|	ЛинейныйТренд.Номенклатура КАК Номенклатура,
	|	ЛинейныйТренд.Клиент КАК Клиент,
	|	ЛинейныйТренд.a КАК a,
	|	ЛинейныйТренд.b КАК b,
	|	ЛинейныйТренд.Порядок КАК Порядок,
	|	ЛинейныйТренд.LastSale КАК LastSale
	|ПОМЕСТИТЬ втВсеПериодыПлюсТренд
	|ИЗ
	|	втПериодыПланирования КАК втПериодыПланирования,
	|	РегистрСведений.ЛинейныйТренд КАК ЛинейныйТренд
	|ГДЕ
	|	ЛинейныйТренд.Период = ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), ГОД, -1)
	|	И ЛинейныйТренд.СглаженныеДанные = &СглаженныеДанные
	|	И ЛинейныйТренд.LastSale = ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, -1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВсеПериодыПлюсТренд.Период КАК Период,
	|	втВсеПериодыПлюсТренд.Клиент КАК Клиент,
	|	втВсеПериодыПлюсТренд.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(втВсеПериодыПлюсТренд.a * (втВсеПериодыПлюсТренд.Порядок + МЕСЯЦ(втВсеПериодыПлюсТренд.Период)) + втВсеПериодыПлюсТренд.b * ЕСТЬNULL(КоэффициентыСезонности.Коэффициент, 1) КАК ЧИСЛО(15, 0)) КАК Прогноз,
	|	ЕСТЬNULL(КоэффициентыСезонности.Коэффициент, 1) КАК КоэффициентСезонности
	|ПОМЕСТИТЬ втИтоги
	|ИЗ
	|	втВсеПериодыПлюсТренд КАК втВсеПериодыПлюсТренд
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыСезонности КАК КоэффициентыСезонности
	|		ПО втВсеПериодыПлюсТренд.Номенклатура.ГруппаСезонности = КоэффициентыСезонности.ГруппаСезонности
	|			И (КоэффициентыСезонности.Месяц = ДОБАВИТЬКДАТЕ(втВсеПериодыПлюсТренд.Период, ГОД, -1))
	|ГДЕ
	|	втВсеПериодыПлюсТренд.LastSale = ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, -1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланируемыйСтартПродаж.Период,
	|	ВЫБОР
	|		КОГДА ПланируемыйСтартПродаж.Клиент = ЗНАЧЕНИЕ(справочник.партнеры.пустаяссылка)
	|			ТОГДА ЗНАЧЕНИЕ(справочник.партнеры.остальные)
	|		ИНАЧЕ ПланируемыйСтартПродаж.Клиент
	|	КОНЕЦ,
	|	ПланируемыйСтартПродаж.Номенклатура,
	|	ВЫРАЗИТЬ(ПланируемыйСтартПродаж.СреднемесячныеПродажи * ЕСТЬNULL(КоэффициентыСезонности.Коэффициент, 1) КАК ЧИСЛО(15, 0)),
	|	ЕСТЬNULL(КоэффициентыСезонности.Коэффициент, 1)
	|ИЗ
	|	РегистрСведений.ПланируемыйСтартПродаж КАК ПланируемыйСтартПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыСезонности КАК КоэффициентыСезонности
	|		ПО ПланируемыйСтартПродаж.Номенклатура.ГруппаСезонности = КоэффициентыСезонности.ГруппаСезонности
	|			И (КоэффициентыСезонности.Месяц = ДОБАВИТЬКДАТЕ(ПланируемыйСтартПродаж.Период, ГОД, -1))
	|ГДЕ
	|	ПланируемыйСтартПродаж.Период >= НАЧАЛОПЕРИОДА(&Период, ГОД)
	|	И ПланируемыйСтартПродаж.Период <= КОНЕЦПЕРИОДА(&Период, ГОД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИтоги.Период КАК Период,
	|	втИтоги.Клиент КАК Клиент,
	|	втИтоги.Номенклатура КАК Номенклатура,
	|	СУММА(втИтоги.Прогноз) КАК Прогноз,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Количество <> 0
	|			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Выручка / ВложенныйЗапрос.Количество КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	втИтоги КАК втИтоги
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДанныеПродаж.Номенклатура КАК Номенклатура,
	|			ВЫБОР
	|				КОГДА НЕ ДанныеПродаж.Клиент.ВИП
	|					ТОГДА ЗНАЧЕНИЕ(справочник.партнеры.остальные)
	|				ИНАЧЕ ДанныеПродаж.Клиент
	|			КОНЕЦ КАК Клиент,
	|			СУММА(ДанныеПродаж.Количество) КАК Количество,
	|			СУММА(ДанныеПродаж.Выручка) КАК Выручка
	|		ИЗ
	|			РегистрСведений.ДанныеПродаж КАК ДанныеПродаж
	|		ГДЕ
	|			ДанныеПродаж.Период МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, -1) И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ), МЕСЯЦ, -1), МЕСЯЦ)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ДанныеПродаж.Номенклатура,
	|			ВЫБОР
	|				КОГДА НЕ ДанныеПродаж.Клиент.ВИП
	|					ТОГДА ЗНАЧЕНИЕ(справочник.партнеры.остальные)
	|				ИНАЧЕ ДанныеПродаж.Клиент
	|			КОНЕЦ) КАК ВложенныйЗапрос
	|		ПО втИтоги.Клиент = ВложенныйЗапрос.Клиент
	|			И втИтоги.Номенклатура = ВложенныйЗапрос.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	втИтоги.Период,
	|	втИтоги.Клиент,
	|	втИтоги.Номенклатура,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Количество <> 0
	|			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Выручка / ВложенныйЗапрос.Количество КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(втИтоги.Прогноз) > 0"
КонецФункции
