&НаКлиенте
Процедура ГрафикПриРедактированииЗначения(Элемент, ЗначениеДиаграммы, СостояниеРедактирования)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	График.РежимРедактированияЗначений = РежимРедактированияЗначенийДиаграммы.Использовать;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТренд(Команда)
	Если НЕ ЗначениеЗаполнено(ТипАппроксимации) Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("НеЗаполненыДанные", ЭтотОбъект), "Не выбран тип аппроксимации",,"Ошибка");
		Возврат;                        	
	КонецЕсли;
	Добавили = Ложь;
	Для Каждого ЭлИндексаСерии из СерияГрафика Цикл
		Добавили = Истина;
		Если ЭлИндексаСерии.Пометка Тогда		
			Серия = График.Серии.Получить(ЭлИндексаСерии.Значение);
			ЛинияТренда = Серия.ЛинииТренда.Добавить();
			ЛинияТренда.ТипАппроксимации = ТипАппроксимацииЛинииТрендаДиаграммы[ТипАппроксимации];
			Если ЛинияТренда.ТипАппроксимации = ТипАппроксимацииЛинииТрендаДиаграммы.Полиномиальный Тогда
				ЛинияТренда.ПорядокАппроксимации = МаксимальнаяСтепеньПолинома;	
			КонецЕсли;
			ЛинияТренда.Маркер = ТипМаркераДиаграммы.Нет;
			ЛинияТренда.ОтображатьУравнение = Истина;
			ЛинияТренда.ОтображатьКоэффициентДетерминации = Истина;
		КонецЕсли;
	КонецЦикла;
	Если НЕ Добавили Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("НеЗаполненыДанные", ЭтотОбъект), "Не выбран ни одна серия",,"Ошибка");
		Возврат;  
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НеЗаполненыДанные(ДополнительныеПараметры) Экспорт
	

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПоИзмененнымДанным = Истина;
	Элементы.ТипАппроксимации.СписокВыбора.Добавить("Линейный");
	Элементы.ТипАппроксимации.СписокВыбора.Добавить("Логарифмический");
	Элементы.ТипАппроксимации.СписокВыбора.Добавить("Полиномиальный");
	Элементы.ТипАппроксимации.СписокВыбора.Добавить("Степенной");
	Элементы.ТипАппроксимации.СписокВыбора.Добавить("Экспоненциальный");
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	График.Очистить();
	СерияГрафика.Очистить();
	СформироватьНаСервере();
КонецПроцедуры



&НаСервере
Функция  ПолучитьТекстЗапросаПоИзмДанным()
Возврат 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДанныеПродаж.Период, МЕСЯЦ) КАК Период,
		|	СУММА(ДанныеПродаж.Количество) КАК Количество,
		|	ДанныеПродаж.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	РегистрСведений.ДанныеПродаж КАК ДанныеПродаж
		|ГДЕ
		|	ДанныеПродаж.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Начало, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Конец, МЕСЯЦ)
		|	И ДанныеПродаж.Номенклатура В(&МассивНоменклатуры)
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(ДанныеПродаж.Период, МЕСЯЦ),
		|	ДанныеПродаж.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДанныеПродажДляПланирования.Период, вт.Период) КАК Период,
		|	ЕСТЬNULL(ДанныеПродажДляПланирования.Номенклатура, вт.Номенклатура) КАК Номенклатура,
		|	ЕСТЬNULL(ДанныеПродажДляПланирования.Количество, вт.Количество) КАК Количество
		|ИЗ
		|	вт КАК вт
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПродажДляПланирования КАК ДанныеПродажДляПланирования
		|		ПО вт.Период = ДанныеПродажДляПланирования.Период
		|			И вт.Номенклатура = ДанныеПродажДляПланирования.Номенклатура
		|ИТОГИ ПО
		|	Номенклатура,
		|	Период ПЕРИОДАМИ(МЕСЯЦ, НАЧАЛОПЕРИОДА(&Начало, МЕСЯЦ), КОНЕЦПЕРИОДА(&Конец, МЕСЯЦ))";
КонецФункции

&НаСервере
Процедура СформироватьНаСервере()
	График.Обновление = Ложь;
	
	Запрос = Новый Запрос;
	Если ПоИзмененнымДанным Тогда
		Запрос.Текст = ПолучитьТекстЗапросаПоИзмДанным();
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДанныеПродаж.Период, МЕСЯЦ) КАК Период,
		|	СУММА(ДанныеПродаж.Количество) КАК Количество,
		|	ДанныеПродаж.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ДанныеПродаж КАК ДанныеПродаж
		|ГДЕ
		|	ДанныеПродаж.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Начало, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Конец, МЕСЯЦ)
		|	И ДанныеПродаж.Номенклатура В(&МассивНоменклатуры)
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(ДанныеПродаж.Период, МЕСЯЦ),
		|	ДанныеПродаж.Номенклатура
		|ИТОГИ ПО
		|	Номенклатура,
		|	Период ПЕРИОДАМИ(МЕСЯЦ, НАЧАЛОПЕРИОДА(&Начало, МЕСЯЦ), КОНЕЦПЕРИОДА(&Конец, МЕСЯЦ))";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивНоменклатуры", Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Начало", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("Конец", Объект.Период.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаТовар.Следующий() Цикл
		
		Серия = График.УстановитьСерию(ВыборкаТовар.Номенклатура);
		Серия.Текст = ВыборкаТовар.Номенклатура.Наименование;
		//Серия.Значение = ВыборкаТовар.Номенклатура;
		ВыборкаПериод = ВыборкаТовар.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПериод.Следующий() Цикл
			
			Выборка = ВыборкаПериод.Выбрать();
			                  
			Пока Выборка.Следующий() Цикл
				Месяц = число(формат(Выборка.Период,"ДФ=M"));
				тчк = СокрП(Сред("        январь  февраль март    апрель  май     июнь    июль    август  сентябрьоктябрь ноябрь  декабрь ",Месяц*8+1,3)) +
				формат(Выборка.Период,"ДФ=' yy'");
				Точка = График.УстановитьТочку(Выборка.Период);
				Точка.Текст = тчк;
				График.УстановитьЗначение(Точка, Серия, Выборка.Количество);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	График.Обновление = Истина;
	
	СерияГрафика.Очистить();
	Для Каждого Серия из График.Серии Цикл
		СерияГрафика.Добавить(График.Серии.Индекс(Серия), Серия.Текст);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТренды(Команда)
	График.Обновление = Ложь;
	Для Каждого Серия из График.Серии Цикл
		МассивИндексов = Новый Массив;
		Для Каждого ЛТ Из Серия.ЛинииТренда	Цикл
			МассивИндексов.Добавить(Серия.ЛинииТренда.Индекс(ЛТ));
		КонецЦикла;
		Для Каждого Инд Из МассивИндексов Цикл
			Серия.ЛинииТренда.Удалить(Инд);
		КонецЦикла;
	КонецЦикла;
	График.Обновление = Истина;
КонецПроцедуры

&НаСервере
Процедура СохранитьИзмененияДанныхНаСервере()
	Для Каждого Точка из График.Точки Цикл
		Для Каждого Серия Из График.Серии Цикл
			ЗначениеДиаграммы = График.ПолучитьЗначение(Точка, Серия);
			ЗаписатьДанные(ЗначениеДиаграммы);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанные(ЗначениеДиаграммы)
	
	НоваяЗапись = РегистрыСведений.ДанныеПродажДляПланирования.СоздатьМенеджерЗаписи();
	
	НоваяЗапись.Период 			= ЗначениеДиаграммы.Точка.Значение;
	НоваяЗапись.Номенклатура 	= ЗначениеДиаграммы.Серия.Значение;
	НоваяЗапись.Количество 		= ЗначениеДиаграммы.Значение;
	
	НоваяЗапись.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзмененияДанных(Команда)
	СохранитьИзмененияДанныхНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Гистограмма(Команда)
	График.ТипДиаграммы = ТипДиаграммы.Гистограмма;
КонецПроцедуры

&НаКлиенте
Процедура График(Команда)
	График.ТипДиаграммы = ТипДиаграммы.График;
КонецПроцедуры
