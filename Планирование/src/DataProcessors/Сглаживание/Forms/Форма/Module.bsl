
&НаКлиенте
Процедура Показать(Команда)
	ПоказатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПоказатьНаСервере()
	Схема = Обработки.Сглаживание.ПолучитьМакет("Макет");
	
	СтрокаВыражения = Схема.ПоляИтога.Найти("Среднее").Выражение;
	СтрокаВыражения = СтрЗаменить(СтрокаВыражения, "30", Формат(ПериодСреднейДни,"ЧДЦ=0; ЧГ=0"));
	СтрокаВыражения = СтрЗаменить(СтрокаВыражения, "31", Формат(ПериодСреднейДни + 1,"ЧДЦ=0; ЧГ=0"));
	Схема.ПоляИтога.Найти("Среднее").Выражение = СтрокаВыражения;
	
	КомпоновщикМакета   = Новый КомпоновщикМакетаКомпоновкиДанных;  
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорВывода     = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;    
	
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема); 
	КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	
	Настройки = Схема.НастройкиПоУмолчанию;	
	
	ПолеОтбораНоменклатуры = Новый ПолеКомпоновкиДанных("Номенклатура");
	ОтборПоНоменклатуре = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборПоНоменклатуре.ЛевоеЗначение    = ПолеОтбораНоменклатуры;
	ОтборПоНоменклатуре.ВидСравнения     = ВидСравненияКомпоновкиДанных.Равно;
	ОтборПоНоменклатуре.ПравоеЗначение   = Номенклатура;
	ОтборПоНоменклатуре.Использование    = Истина; 
	
	
	Период = Настройки.ПараметрыДанных.Элементы.Найти("Период"); 
	ВыбПериод = Новый СтандартныйПериод;
	ВыбПериод.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	ВыбПериод.ДатаНачала = НачалоГода(Год);
	ВыбПериод.ДатаОкончания = КонецГода(Год);
	
	Период.Значение = ВыбПериод;   
	Период.Использование = Истина;

	Макет = КомпоновщикМакета.Выполнить(Схема, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
    
	ПроцессорКомпоновки.Инициализировать(Макет,,,Истина);
	тз = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(тз);

	ПроцессорВывода.Вывести(ПроцессорКомпоновки,Истина); 
	ПостроитьГрафик(тз);
КонецПроцедуры

&НаСервере
Процедура ПостроитьГрафик(тз)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(тз.Период КАК ДАТА) КАК Период,
		|	тз.Количество КАК Количество,
		|	тз.Среднее КАК Среднее
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	&тз КАК тз
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(вт.Период, НЕДЕЛЯ) КАК Период,
		|	СУММА(вт.Количество) КАК Количество,
		|	СУММА(вт.Среднее) КАК Среднее
		|ИЗ
		|	вт КАК вт
		|ГДЕ
		|	вт.Период >= &ДатаНачала
		|	И вт.Период <= &ДатаОкончания
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(вт.Период, НЕДЕЛЯ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), НЕДЕЛЯ) КАК Период,
		|	СУММА(вт.Среднее) КАК Среднее
		|ИЗ
		|	вт КАК вт
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), НЕДЕЛЯ) >= &ДатаНачала
		|	И НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), НЕДЕЛЯ) <= &ДатаОкончания
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(вт.Период, ДЕНЬ, &Сдвиг), НЕДЕЛЯ)";
	
	Если Данные = 1 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕДЕЛЯ", "МЕСЯЦ");
	КонецЕсли;

	Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(Год));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецГода(Год));
	Запрос.УстановитьПараметр("тз", тз);
	Сдвиг = - Цел(ПериодСреднейДни/2);
	Запрос.УстановитьПараметр("Сдвиг", Сдвиг);

	Результат = Запрос.ВыполнитьПакет();
	
	График.Обновление = Ложь;
	График.Очистить();
	
	СерияПродажи = График.УстановитьСерию("СерияПродажи");
	СерияПродажи.Текст = "Реальные продажи";
		
	СерияСредние = График.УстановитьСерию("СерияСредние");
	СерияСредние.Текст = "Скользящая средняя";
	
	СерияСредниеСоСдвигом = График.УстановитьСерию("СерияСредниеСоСдвигом");
	СерияСредниеСоСдвигом.Текст = "Скользящая средняя сдвиг " + Сдвиг;
	
	Для Каждого СерияДиаграммы Из График.Серии Цикл
		СерияДиаграммы.Маркер = ТипМаркераДиаграммы.Нет;
	КонецЦикла;

	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		Точка = График.УстановитьТочку(Выборка.Период);
		Точка.Текст = Формат(Выборка.Период,"ДФ=dd.MM.yy");
		График.УстановитьЗначение(Точка, СерияПродажи, Выборка.Количество);
		График.УстановитьЗначение(Точка, СерияСредние, Выборка.Среднее);
	КонецЦикла;
	
	Выборка2 = Результат[2].Выбрать();
	Пока Выборка2.Следующий() Цикл
		Точка = График.УстановитьТочку(Выборка2.Период);
		Точка.Текст = Формат(Выборка2.Период,"ДФ=dd.MM.yy");
		График.УстановитьЗначение(Точка, СерияСредниеСоСдвигом, Выборка2.Среднее);
	КонецЦикла;
	
	График.Обновление = Истина;
	
КонецПроцедуры



	